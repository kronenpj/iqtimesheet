apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getDateNow = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'date', '+%Y.%m.%d'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

task clearData(type: Exec) {
    def clearDataCommand = ['adb', 'shell', 'pm', 'clear',
                            'com.github.kronenpj.iqtimesheet.IQTimeSheet']
    commandLine clearDataCommand
}

android {
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    compileSdkVersion 28
    buildToolsVersion '29.0.0'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 20
        versionName "2019-beta1"
        applicationId 'com.github.kronenpj.iqtimesheet.IQTimeSheet'
        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
        buildConfigField "String", "BuildDate", "\"${getDateNow()}\""
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main']
            kotlin.srcDirs = ['src/main']
            res.srcDirs = ['res']
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    //implementation "io.requery:requery:$requeryVersion"
    //implementation "io.requery:requery.kotlin:$requeryVersion"
    //implementation "io.requery:requery.android:$requeryVersion"
    //kapt "io.requery:requery.processor:$requeryVersion"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.anko:anko:$anko_version"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
    testImplementation 'org.amshove.kluent:kluent:1.15'
//    androidTestImplementation 'com.android.support.test:runner:1.1.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

buildscript {
    dependencies {
        classpath "com.android.tools.build:gradle:$android_build_gradle"
        classpath 'io.fabric.tools:gradle:1.+'
    }
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
}
